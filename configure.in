dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(gtk-gnutella, 0.85)
AM_CONFIG_HEADER(config.h)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_PROG_YACC

dnl Check whether libresolv is needed for inet_aton()
need_lresolv=yes
AC_MSG_CHECKING(whether inet_aton needs libresolv)
AC_TRY_LINK_FUNC(inet_aton, need_lresolv=no, LIBS="$LIBS -lresolv")
AC_MSG_RESULT($need_lresolv)

AM_PATH_GTK(1.2.0, ,
            AC_MSG_ERROR(Cannot find GTK: Is gtk-config in path?))

dnl Set PACKAGE_DATA_DIR in config.h.
if test "x${datadir}" = 'x${prefix}/share'; then
  if test "x${prefix}" = "xNONE"; then
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${ac_default_prefix}/share/${PACKAGE}")
  else
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${prefix}/share/${PACKAGE}")
  fi
else
  AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${datadir}/${PACKAGE}")
fi

dnl Set PACKAGE_SOURCE_DIR in config.h.
packagesrcdir=`cd $srcdir && pwd`
AC_DEFINE_UNQUOTED(PACKAGE_SOURCE_DIR, "${packagesrcdir}")

dnl ---------------------------------------------------------------------------
dnl Handle user-specified configure options
dnl ---------------------------------------------------------------------------

AC_ARG_ENABLE(no-rfc1918,[
gtk-gnutella options:
  --enable-no-rfc1918             avoid RFC1918 networks (default=off)], , enable_no_rfc1918=no)
AC_ARG_ENABLE(xml-store,[
  --enable-xml-store              save searches and filters in XML (default=on)], , enable_xml_store=no)


AC_MSG_CHECKING([whether to enable RFC1918 network avoidance mode])
if test "x$enable_no_rfc1918" = "xno"
then
	AC_MSG_RESULT([no])
else
	CFLAGS="$CFLAGS -DNO_RFC1918"
	AC_MSG_RESULT([yes])
fi

AC_PATH_PROG(XML_CONFIG,xml2-config,no)
if test "x$XML_CONFIG" != "xno"; then
    AC_MSG_CHECKING([whether to enable XML persistance backend])
    if test "x$enable_xml_store" = "xno"
    then
    	AC_MSG_RESULT([no])
    else
        AC_MSG_RESULT([yes])
        AC_MSG_CHECKING(libxml2 version)
        libxml2_version=`$XML_CONFIG --version | awk '{print $1;}'`
        AC_MSG_RESULT($libxml2_version)
        libxml2_version=`echo $libxml2_version | awk -F. '{print $1 * 10000 + $2 * 100 + $3;}'`
        if test 0$libxml2_version -lt 20419; then
            AC_MSG_ERROR(libxml2 2.4.19 or later required)
        fi
        XML_LIBS=`$XML_CONFIG --libs`
        XML_CFLAGS=`$XML_CONFIG --cflags`
        AC_SUBST(XML_LIBS)
        AC_SUBST(XML_CFLAGS)
        LIBXML2_REQUIRED=2.4.19
        AC_SUBST(LIBXML2_REQUIRED)
        AC_DEFINE(USE_SEARCH_XML)
    fi
else
    AC_MSG_CHECKING([whether to enable XML persistance backend])
   	AC_MSG_RESULT([no])
    enable_xml_store="no"
fi
AM_CONDITIONAL(ENABLE_XML_STORE, test "x$enable_xml_store" = "xyes")


dnl Use -Wall if we have gcc.
changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

AC_CHECK_FUNCS(herror inet_aton)

dnl Checks for sys/sendfile.h
AC_CHECK_HEADER(sys/sendfile.h, [have_sendfile_h=yes], [have_sendfile_h=no])
if test "$have_sendfile_h" = yes; then
	AC_DEFINE(HAVE_SENDFILE_H)
fi

dnl Checks for ctype.h
AC_CHECK_HEADER(ctype.h, [have_ctype_h=yes], [have_ctype_h=no])
if test "$have_ctype_h" = yes; then
	AC_DEFINE(HAVE_CTYPE_H)
fi

AC_OUTPUT([
Makefile
src/Makefile
gtk-gnutella.spec
])

